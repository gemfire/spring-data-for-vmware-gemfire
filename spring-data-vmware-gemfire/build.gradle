// Copyright (c) VMware, Inc. 2022. All rights reserved.
// SPDX-License-Identifier: Apache-2.0
import com.google.cloud.storage.BlobId
import com.google.cloud.storage.BlobInfo
import com.google.cloud.storage.Storage
import com.google.cloud.storage.StorageOptions

buildscript {
  dependencies {
    classpath 'com.google.cloud:google-cloud-storage:2.29.1'
  }
}

plugins {
  id 'java-library'
  id "io.freefair.lombok" version "8.0.1"
  id 'gemfire-repo-artifact-publishing'
  id 'spring-java-jar'
}

java {
  withJavadocJar()
  withSourcesJar()
}

javadoc {
  title = "Spring Data ${getSpringDataBaseVersion()} for VMware GemFire ${getGemFireBaseVersion()} Java API Reference"
  failOnError=false
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

project.ext.set("pomProjectLongName","Spring Data VMware GemFire")
project.ext.set("pomProjectArtifactName","spring-data-2.7-gemfire-${getGemFireBaseVersion()}")
project.ext.set("pomProjectDescription","Spring Data For VMware GemFire")

dependencies {
  compileOnly "com.vmware.gemfire:geode-core:$gemfireVersion"
  compileOnly "com.vmware.gemfire:geode-logging:$gemfireVersion"
  compileOnly "com.vmware.gemfire:geode-cq:$gemfireVersion"
  compileOnly "com.vmware.gemfire:geode-wan:$gemfireVersion"
  compileOnly "com.vmware.gemfire:geode-gfsh:$gemfireVersion"
  compileOnly "com.vmware.gemfire:geode-lucene:$gemfireVersion"
  compileOnly "com.vmware.gemfire:geode-tcp-server:$gemfireVersion"
  compileOnly "com.vmware.gemfire:geode-deployment-legacy:$gemfireVersion"

  implementation 'javax.cache:cache-api:1.1.1'
  api 'org.springframework:spring-context-support:5.3.30'
  api 'org.springframework:spring-tx:5.3.30'
  api 'org.springframework:spring-web:5.3.30'
  api "org.springframework.data:spring-data-commons:$springDataVersion"
  implementation 'org.apache.shiro:shiro-spring:1.12.0'
  implementation 'org.aspectj:aspectjweaver:1.9.20.1'
  implementation 'com.fasterxml.jackson.core:jackson-annotations:2.14.3'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.3'
  runtimeOnly 'antlr:antlr:2.7.7'
  compileOnly('javax.enterprise:cdi-api:1.2') {
    exclude group: 'javax.annotation', module: 'jsr250-api'
  }


  testImplementation "com.vmware.gemfire:geode-core:$gemfireVersion"
  testImplementation "com.vmware.gemfire:geode-logging:$gemfireVersion"
  testImplementation "com.vmware.gemfire:geode-cq:$gemfireVersion"
  testImplementation "com.vmware.gemfire:geode-wan:$gemfireVersion"
  testImplementation "com.vmware.gemfire:geode-gfsh:$gemfireVersion"
  testImplementation "com.vmware.gemfire:geode-lucene:$gemfireVersion"
  testImplementation "com.vmware.gemfire:geode-tcp-server:$gemfireVersion"
  testImplementation "com.vmware.gemfire:geode-deployment-legacy:$gemfireVersion"

  testImplementation 'org.apache.geronimo.specs:geronimo-jcdi_2.0_spec:1.0.1'
  testImplementation 'javax.interceptor:javax.interceptor-api:1.2.2'
  testImplementation 'javax.el:el-api:2.2'
  testImplementation 'ch.qos.logback:logback-classic:1.2.12'
  testImplementation 'org.apache.logging.log4j:log4j-to-slf4j:2.17.2'
  testImplementation 'javax.annotation:javax.annotation-api:1.3.2'
  testImplementation 'org.apache.derby:derbyLocale_zh_TW:10.9.1.0'
  testImplementation 'org.apache.openwebbeans:openwebbeans-se:2.0.27'
  testImplementation 'org.assertj:assertj-core:3.21.0'
  testImplementation 'org.iq80.snappy:snappy:0.4'
  testImplementation 'org.springframework:spring-test:5.3.30'

  testImplementation('com.vmware.gemfire:spring-test-gemfire-9.15-2.7:1.0.0')
  testImplementation('org.springframework.shell:spring-shell:1.2.0.RELEASE') {
    exclude group: 'com.google.guava', module: 'guava'
  }
  testImplementation('org.mockito:mockito-core:3.12.4') {
    force = true
  }
  testImplementation('javax.enterprise:cdi-api:1.0') {
    exclude group: 'javax.annotation', module: 'jsr250-api'
  }

  testImplementation 'org.awaitility:awaitility:4.2.0'
}

java.sourceCompatibility = JavaVersion.VERSION_1_8
java.targetCompatibility = JavaVersion.VERSION_1_8

test {
  forkEvery = 1
//  maxParallelForks = 1

  systemProperty "java.util.logging.config.file", "${project.buildDir}/test-classes/java-util-logging.properties"
  systemProperty "javax.net.ssl.keyStore", "${project.buildDir}/test-classes/trusted.keystore"
  systemProperty "gemfire.disableShutdownHook", "true"
  systemProperty "logback.log.level", "error"
  systemProperty "spring.profiles.active", "apache-geode"

  filter {
    includeTestsMatching "*.*Tests"
    includeTestsMatching "*.*Test"
  }
}

repositories {
  def additionalMavenRepoURLs = project.ext.get('additionalMavenRepoURLs')
  if (!additionalMavenRepoURLs.isEmpty() && !additionalMavenRepoURLs.isBlank()) {
    additionalMavenRepoURLs.split(',').each {
      project.getRepositories()
              .maven(mavenRepository -> {
                mavenRepository.setUrl(uri(it.toString()));
              });
    }
  }
}

String getGemFireBaseVersion() {
  return getBaseVersion(gemfireVersion)
}

String getSpringDataBaseVersion() {
  return getBaseVersion(springDataVersion)
}

static String getBaseVersion(String version) {
  def split = "${version}".split("\\.")
  if (split.length < 2) {
    throw new RuntimeException("version '$version' is malformed")
  }
  return "${split[0]}.${split[1]}"
}

tasks.register('copyJavadocsToBucket') {
  dependsOn javadocJar
  doLast {
    Storage storage = StorageOptions.newBuilder().setProjectId(project.findProperty('docsGCSProject')).build().getService()
    BlobId blobId = BlobId.of(project.findProperty("docsGCSBucket"), "${project.findProperty('pomProjectArtifactName')}/${project.version}/${javadocJar.outputs.files.singleFile.name}")
    BlobInfo blobInfo = BlobInfo.newBuilder(blobId).build()
    storage.createFrom(blobInfo, javadocJar.outputs.files.singleFile.toPath())
  }
}
